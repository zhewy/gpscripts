// this function ignores notes after a set time of sustain pedal being down
// allows layering not to build up
  
Var
    sustainPedalDownState : boolean = false
    pedalDownTime : Double
    sustainPedalPassThruTime ("Time till ignore notes") : Parameter 100..2000 = 500 // time to ignore
    playHeldKeyVelocity ("held key velocity") : Parameter 1..127 = 40
    collectKeysAfterPedal ("collect keys") : Parameter "Disabled", "Enabled" = "Enabled" // disable/enable collect and play after pedal down
    noteVelocities : Integer[128]
    ntPending : NoteTracker

Function SendAllNoteOnIfNeeded()
  var
    PendingNotes : Integer Array
    i,n : Integer
    
  PendingNotes = NoteTracker_GetHeldNotes(ntPending)
  
  for i=0 ; i<Size(PendingNotes); i=i+1 Do
    SendNow(MakeNoteMessage(PendingNotes[i], noteVelocities[PendingNotes[i]]))
  End

  NoteTracker_Clear(ntPending)
End

//Called when a NoteOn message is received
On ControlChangeEvent(m : ControlChangeMessage) Matching 64
  var
    ccValue : Integer = m.GetCCValue()
    
  If sustainPedalDownState == true then
    If ccValue < 64 then
      sustainPedalDownState = false
      If collectKeysAfterPedal == "Enabled" then
        SendAllNoteOnIfNeeded()
      End
    End
  Else // sustainPedalDownState == false
    If ccValue >= 64 then
      sustainPedalDownState = true
      pedalDownTime = TimeSinceStartup()
    End
  End

  SendNow(m)
End

On NoteOnEvent(m : NoteMessage)
  noteVelocities[GetNoteNumber(m)] = GetVelocity (m)

  If sustainPedalDownState == true then
    If TimeSinceStartup() - pedalDownTime < sustainPedalPassThruTime then
       SendNow(m)
    Else
       NoteTracker_GotNote(ntPending,m)
    End
  Else
    SendNow(m)
    NoteTracker_GotNote(ntPending,m)
  End

End

On NoteOffEvent(m : NoteMessage)
  NoteTracker_GotNote(ntPending,m)  
  SendNow(m)
End

